\cancel mode verify

!**************************************************************
! Description:
!
! Usage: go mapiso_stereo_north GRID CENTRAL_MERIDIAN 2DVAR [mar=40] [SHADE or CONTOUR command + qualifiers] [polygons file] [polygons file] 
!
! Example:
! yes? go mapiso_stereo_north ORCA2.0 0 sosstsst[l=1] 30 shade/lev=20 earth_continents graticule_30s
! yes? go mapiso_stereo_north ORCA2.0 0 sosstsst[l=1] 30 fill/lev=20 earth_continents graticule_30s
!
! Notes:
!
! Calls:
!
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! History:
! Modification:
!
!**************************************************************
define symbol ME_GRID = $1
let ME_CM = $2
define symbol ME_margin = $4%40%
define symbol ME_cmd = $5%shade%/noaxis/hlim=-1:1/vlim=-1:1

!**************************************************************
let ME_VAR = $3

if ($ME_GRID%|COARDS>true|*>false%) then
	let ME_LON=x[g=ME_VAR]*y[g=ME_VAR]*0+x[g=ME_VAR]
	let ME_LAT=x[g=ME_VAR]*y[g=ME_VAR]*0+y[g=ME_VAR]
else
	set data/save
	use "($ME_GRID)_grid.nc"
	set data/restore
	let ME_LON=lon[d=($ME_GRID)_grid.nc]
	let ME_LAT=lat[d=($ME_GRID)_grid.nc]
endif

go my_mp_stereographic_north ME_CM 90 ME_LON ME_LAT

let ME_VAR1=ME_VAR*ME_mp_mask
set variable/title="`ME_VAR,return=title`"/units="`ME_VAR,return=units`" ME_VAR1

! ratio width/height=1
def sym ME_ratio = 1 
def sym ME_plot_width = `(1-($ME_margin)/100)*min(($vp_width),($ME_ratio)*($vp_height))`
def sym ME_plot_height = `($ME_plot_width)/($ME_ratio)`
ppl axlen ($ME_plot_width), ($ME_plot_height)
ppl origin `(($vp_width)-($ME_plot_width))/2`,`(($vp_height)-($ME_plot_height))/2` 

($ME_cmd) ME_VAR1, ME_x_page, ME_y_page

if ($ME_GRID%|COARDS>false|*>true%) then
	cancel data ($ME_GRID)_grid.nc
endif

!**************************************************************
! overlay 1st polygons file 
if $6%false|*>true% then
   go cont_stereo_north `ME_CM` /over `($ME_margin)` $6
endif

!**************************************************************
! overlay 2nd polygons file 
if $7%false|*>true% then
   ! Redefinitions because script cont_ortho.jnl has cancelled 
   ! all ME* variables and ME_* symbols
   define symbol ME_margin = $4%40%    
   let ME_CM = $2
   go cont_stereo_north `ME_CM` /over `($ME_margin)` $7
endif

!**************************************************************
cancel variable ME_*
cancel symbol ME_*

!**************************************************************
set mode/last verify


