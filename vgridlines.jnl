\cancel mode verify

!**************************************************************
! Description: Plot over the vertical grid of a plot
!
! Usage: go vgridlines hdelta [PLOT qualifiers]
!
! Example:
!  yes? plot/i=1:100 sin(i/5)
!  yes? go vgridlines 20 
!  will plot every 20 on the physical xaxis a vertical bar
!  yes? plot/i=1:100 sin(i/5)
!  yes? go vgridlines 20 /dash=(0.3,0.1,0.3,0.1)/color=red/thick=1
!
! Notes:
!
! Calls:
!
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go vgridlines hdelta [plot qualifiers]%

def sym ME_qualifiers=$2%/DASH=(0.1,0.05,0.1,0.05)%

! y1 = first horizontal line (superior value)
if `($YAXIS_MIN) lt 0` then
   def sym ME_y1 = int(($YAXIS_MIN)/$1)*$1
else
   def sym ME_y1 = (int(($YAXIS_MIN)/$1)+1) *$1
endif
! y2 = last horizontal line  (inferior value)
if `($YAXIS_MAX) lt 0` then
   def sym ME_y2 = (int(($YAXIS_MAX)/$1)-1) *$1
else
   def sym ME_y2 = (int(($YAXIS_MAX)/$1)) *$1
endif
def sym ME_yrange = `(($ME_y2)) - (($ME_y1))`

def sym ME_y = ($ME_y1) + (i-1)*$1
def sym ME_nby = int(($ME_yrange)/$1) + 1

if `($ME_nby) ge 1` then
  repeat/i=1:`($ME_nby)`:1 ( plot/nolab/over/vs($ME_qualifiers) {($XAXIS_MIN),($XAXIS_MAX)},{`($ME_y)`,`($ME_y)`} )
endif

!**************************************************************
cancel sym ME_*

!**************************************************************
set mode/last verify

