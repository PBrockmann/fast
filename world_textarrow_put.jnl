\cancel mode verify

!**************************************************************
! Description: Put a text and an arrow at a position expressed in 
!  units of world coordinates (units of your plot)
!
! Usage: go world_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]
!
! Example: 
!  yes? go page_new 1 2
!  yes? set v 1
!  yes? plot/vlim=1:100 i[i=1:100]
!  yes? go world_textarrow_put 80 40 50 80 "textright     _" 1
!  yes? go world_textarrow_put 50 40 40 80 "textcentered" 0
!  yes? go world_textarrow_put 30 40 60 80 "       textleft" 1
!
! Notes:
!  * Arrow size head is expressed in cm
!  * Add a blank before or after text to get spaces between
!  the arrow and the text; to add blanks after the text, use
!  the special character '_' (lowerscript)  
!  yes? go world_textarrow_click "a text with blanks after     _"
!  * Arrow head filling works only over the plot domain between axis
!  * ($XAXIS_MIN),($XAXIS_MAX),($YAXIS_MIN),($YAXIS_MAX)
!  should be defined; if not you can force then to be defined
!  with qualifiers hlimits and vlimits 
!
! Calls: 
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2009/05/14 15:04:00 $ 
! $Name: FAST_608_1_3 $
! $Revision: 1.1.1.26 $
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go world_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $2%<Usage: go world_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $3%<Usage: go world_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $4%<Usage: go world_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%

!**************************************************************
def sym ME_xpos1=$1
def sym ME_ypos1=$2
def sym ME_xpos2=$3
def sym ME_ypos2=$4

def sym ME_text=$5
def sym ME_just=$6%0%
def sym ME_labheight=`$7%0.3%/2.54`   ! cm to inch
def sym ME_angle=$8%0%

def sym ME_ydin=`abs((($YAXIS_MAX))-(($YAXIS_MIN)))`

! text
if `($ME_ypos1) lt ($ME_ypos2)` then
  label ($ME_xpos1),`($ME_ypos1)-2*($ME_ydin)*($ME_labheight)/($PPL$YLEN)`,($ME_just),($ME_angle),($ME_labheight),"($ME_text)"
else
  label ($ME_xpos1),`($ME_ypos1)+($ME_ydin)*($ME_labheight)/($PPL$YLEN)`,($ME_just),($ME_angle),($ME_labheight),"($ME_text)"
endif

! arrow
!-----------------------
! take into account ratio (= ratioworld * ratioviewp)
! to respect in every case the head angle choosen
!-----------------------
def sym ME_ratioworld = `((($YAXIS_MAX))-(($YAXIS_MIN)))/((($XAXIS_MAX))-(($XAXIS_MIN)))`
def sym ME_ratioviewp = `($PPL$XLEN)/($PPL$YLEN)`
def sym ME_ratio = `($ME_ratioworld)*($ME_ratioviewp)`
!say ($ME_ratioworld) ($ME_ratioviewp) ($ME_ratio)

!**************************************************************
def sym ME_xpos1=$1
def sym ME_ypos1=$2
def sym ME_xpos2=$3
def sym ME_ypos2=$4

! arrow
!**************************************************************
let ME_PI = atan(1.)*4.
let ME_DEG2RAD = ME_PI/180.

!**************************************************************
def sym ME_nbvectors=1

let ME_x1 = ($ME_xpos1)
let ME_y1 = ($ME_ypos1) 
let ME_x2 = ($ME_xpos2)
let ME_y2 = ($ME_ypos2) 
let ME_u = `($ME_xpos2) - ($ME_xpos1)` 
let ME_v = `($ME_ypos2) - ($ME_ypos1)`

!-----------------------
! vector points from x1,y1 with components u,v
!           2 
!         / | \
!        a  |  b
!           | 
!           | 
!           | 
!           1 
!-----------------------
def sym ME_head_size=$9%0.3%                 ! in cm
let ME_head_angle = 30*ME_DEG2RAD            ! in degrees
let ME_size = `((($XAXIS_MAX))-(($XAXIS_MIN)))*($ME_head_size)/(($PPL$XLEN)*2.54)`
let ME_alpha = atan2(ME_v/($ME_ratio),ME_u)

!-- point a
let ME_head_xa0 = cos(ME_alpha - ME_head_angle) * ME_size * 1/cos(ME_head_angle)
let ME_head_ya0 = sin(ME_alpha - ME_head_angle) * ME_size * 1/cos(ME_head_angle)

let ME_head_xa1 = ME_x2 - ME_head_xa0  
let ME_head_ya1 = ME_y2 - ME_head_ya0 * ($ME_ratio)

!-- point b
let ME_head_xb0 = sin(ME_alpha + ME_head_angle - ME_PI/2.) * ME_size * 1/cos(ME_head_angle)
let ME_head_yb0 = cos(ME_alpha + ME_head_angle - ME_PI/2.) * ME_size * 1/cos(ME_head_angle)

let ME_head_xb1 = ME_x2 + ME_head_xb0 
let ME_head_yb1 = ME_y2 - ME_head_yb0 * ($ME_ratio)

! arrow color
ppl pen 1,($ME_linecolor)

! line s-e
ppl aline 1,($ME_xpos1),($ME_ypos1),($ME_xpos2),($ME_ypos2)

! line e-a
ppl aline 1,($ME_xpos2),($ME_ypos2),`ME_head_xa1`,`ME_head_ya1`

! line e-b
ppl aline 1,($ME_xpos2),($ME_ypos2),`ME_head_xb1`,`ME_head_yb1`

! line a-b
!ppl aline 1,`ME_head_xa1`,`ME_head_ya1`,`ME_head_xb1`,`ME_head_yb1`

! Return to normal
ppl pen 1,1
 
!**************************************************************
cancel sym ME_*
cancel var ME_*

!**************************************************************
set mode/last verify
