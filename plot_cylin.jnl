\cancel mode verify

!**************************************************************
! Description: Plot longitude,latitude positions in cylindrical projection
!
! Usage: go plot_cylin CENTRAL_MERIDIAN LONVAR LATVAR [POLYGON qualifiers] [marg=30]
!
! Notes:
!  * Pen numbers (/line=x qualifier) range from 1 to 18 and follow the formula:
!          color_thickness = 6 * (thickness - 1) + color
!  with color = 1,2,3,4,5,6 = black,red,green,blue,cyan,magenta
!  and thickness = 1,2,3
!
! Calls: go unlabels
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2009/05/14 15:04:00 $ 
! $Name: FAST_608_1_3 $
! $Revision: 1.1.1.26 $
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go plot_cylin CENTRAL_MERIDIAN LONVAR LATVAR [PLOT qualifiers] [marg=30]%
query/ignore $2%<Usage: go plot_cylin CENTRAL_MERIDIAN LONVAR LATVAR [PLOT qualifiers] [marg=30]%
query/ignore $3%<Usage: go plot_cylin CENTRAL_MERIDIAN LONVAR LATVAR [PLOT qualifiers] [marg=30]%

set data/save

!**************************************************************
define symbol ME_margin = $5%30%

if $4"false|*>true" then
   define symbol ME_QUALIFIERS = /vs/line=1/hlimits=\`ME_CM-180\`:\`ME_CM+180\`:30/vlimits=-90:90:30$4
else
   define symbol ME_QUALIFIERS = /vs/line=1/hlimits=\`ME_CM-180\`:\`ME_CM+180\`:30/vlimits=-90:90:30
endif

!**************************************************************
! ratio width/height=2
def sym ME_ratio = 2
def sym ME_plot_width = `(1-($ME_margin)/100)*min(($vp_width),($ME_ratio)*($vp_height))`
def sym ME_plot_height = `($ME_plot_width)/($ME_ratio)`
ppl axlen ($ME_plot_width), ($ME_plot_height)
ppl origin `(($vp_width)-($ME_plot_width))/2`,`(($vp_height)-($ME_plot_height))/2`

!**************************************************************

let ME_CM = $1
 
let ME_Longitude = xsequence(($2)) 
let ME_Latitude = xsequence(($3)) 
 
plot/set_up($ME_QUALIFIERS) ME_Longitude,ME_Latitude

go unlabels 1 8

ppl xfor (I7,''LONE'')		! set longitude axis labels
ppl yfor (I7,''LAT'')		! set latitude axis labels

ppl set ME_QUALIFIERS1 $edit(ME_QUALIFIERS,upcase)
ppl set ME_ISOVER $locate("/OVER",ME_QUALIFIERS1)

if `($ME_ISOVER) gt 0` then
   ppl plot/line/over
else
   ppl plot
endif

!**************************************************************
set data/restore

!**************************************************************
cancel variable ME_* 
cancel symbol ME_* 

!**************************************************************
set mode/last verify

