\cancel mode verify

!**************************************************************
! Description: Define a variable with a longitude axis with 2 extra columns
!              needed to create continuous contours
!
! Usage: go def_var_with_2_extra_points_on_xaxis.jnl var1 var2
!
! Example:
!        yes? use levitus_climatology.cdf
!        yes? let var1=temp[k=1]
!        yes? set w 1
!        yes? set v upper
!        yes? go mapiso_ortho COARDS 20 40 var1 30 fill/lev=25 earth_continents graticule_30s
!        yes? go def_var_with_2_extra_points_on_xaxis.jnl var1 var2
!        yes? show grid var1
!        yes? show grid var2
!        yes? set v lower
!        yes? go mapiso_ortho COARDS 20 40 var2 30 fill/lev=25 earth_continents graticule_30s
!        
! Notes:
!
! Calls:
!
! Author: Patrick Brockmann
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! History:
! Modification:
!
!**************************************************************

let xaxis=x[g=($01)]
def sym isize=`xaxis,return=isize`

let xboxlon=xbox[g=($01)]
let xboxlolon=xboxlo[g=($01)]

def sym xboxfirst=`xboxlon[i=1]`
def sym xboxlast=`xboxlon[i=($isize)]`

! Create boundaries for /edges axis definition
let xboxlolon1=xcat(xboxlolon,xboxlolon[i=1]+360)

! Duplicate first
let xboxlolon2=xcat(xboxlolon1[i=($isize)]-360,xboxlolon1)
! Duplicate last
let xboxlolon3=xcat(xboxlolon2,xboxlolon1[i=2]+360)

def axis/x/from_data/edges/modulo/units="degrees_east" mynewaxis=xboxlolon3

!show axis mynewaxis

let/title="`($01),return=title`"/units="`($01),return=units`" ($02)=($01)[gx=mynewaxis@NRST]

!**************************************************************
set mode/last verify



