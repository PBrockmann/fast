\cancel mode verify

!**************************************************************
! Description: Plot continents file in cylindrical projection
!
! Usage: go cont_cylin CENTRAL_MERIDIAN [POLYGON qualifiers] [marg=30] [polygons file=earth_continents]
!
! Notes:
!  * Pen numbers (/line=x qualifier) range from 1 to 18 and follow the formula:
!          color_thickness = 6 * (thickness - 1) + color
!  with color = 1,2,3,4,5,6 = black,red,green,blue,cyan,magenta
!  and thickness = 1,2,3
!  * polygons file = specify xxxxx to use an existing polygon file named polydouble_xxxxx.nc
!                    default is earth_continents
!  * margin will be used only if qualifiers /over is not present 
!
! Calls: go unlabels
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr  
! History:
!  * The classic "go land" script gives continents with some 
!  discontinuties. Here there are 2 world maps continuous
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go cont_cylin CENTRAL_MERIDIAN [POLYGON qualifiers] [marg=30] [polygons file=earth_continents]%

set data/save

!**************************************************************
define symbol ME_margin = $3%30%

if $2"false|*>true" then
        define symbol ME_QUALIFIERS = /line=1/hlimits=\`ME_CM-180\`:\`ME_CM+180\`:30/vlimits=-90:90:30$2
else
        define symbol ME_QUALIFIERS = /line=1/hlimits=\`ME_CM-180\`:\`ME_CM+180\`:30/vlimits=-90:90:30
endif

!**************************************************************
ppl set ME_QUALIFIERS1 $edit(ME_QUALIFIERS,upcase)
ppl set ME_ISOVER $locate("/OVER",ME_QUALIFIERS1)
ppl set ME_ISFILL $locate("/FILL",ME_QUALIFIERS1)

!**************************************************************
if `($ME_ISOVER) le 0` then
! ratio width/height=2
	def sym ME_ratio = 2
	def sym ME_plot_width = `(1-($ME_margin)/100)*min(($vp_width),($ME_ratio)*($vp_height))`
	def sym ME_plot_height = `($ME_plot_width)/($ME_ratio)`
	ppl axlen ($ME_plot_width), ($ME_plot_height)
	ppl origin `(($vp_width)-($ME_plot_width))/2`,`(($vp_height)-($ME_plot_height))/2`
endif

!**************************************************************
use polydouble_$4%earth_continents%.nc

let ME_CM = $1

set variable/title=LONGITUDE CONT_LON 
set variable/title=LATITUDE CONT_LAT

polygon/set_up($ME_QUALIFIERS) CONT_LON,CONT_LAT

go unlabels 1 8

ppl xfor (I7,''LONE'')		! set longitude axis labels
ppl yfor (I7,''LAT'')		! set latitude axis labels

if `($ME_ISFILL) gt 0 and ($ME_ISOVER) gt 0` then
   ppl fillpol/line/fill/over
elif `($ME_ISFILL) gt 0 and ($ME_ISOVER) le 0` then
   ppl fillpol/line/fill
elif `($ME_ISFILL) le 0 and ($ME_ISOVER) gt 0` then
   ppl fillpol/line/over
else
   ppl fillpol/line
endif

cancel data polydouble_$4%earth_continents%.nc

!**************************************************************
set data/restore

!**************************************************************
cancel variable ME_* 
cancel symbol ME_* 

!**************************************************************
set mode/last verify

