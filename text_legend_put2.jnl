\cancel mode verify

!**************************************************************
! Description: Put a text preceeding by a line (=legend) expressed as 
!  percentage of width and height of the viewport over a plot  
!
! Usage: go text_legend_put2 xpos1 xpos2 ypos " text" [PLOT qualifiers] [character height=0.3] [time variable]
!
! Example:
!  yes? plot i[i=1:100]
!  yes? plot/over/nolab randu(i[i=1:100])*100
!  yes? plot/over/nolab randn(i[i=1:100])*50
!  yes? plot/over/nolab randn(i[i=1:100])*100
!  yes? go text_legend_put2 10 20 13 " text1" /color=1/line 
!  yes? go text_legend_put2 10 20 10 " text2" /color=2/line 
!  yes? go text_legend_put2 10 20  7 " @C004text3" /color=4/line
!  yes? go text_legend_put2 10 20  4 " text4" /color=3/line 
!
!  yes? use monthly_navy_winds
!  yes? set window 1
!  yes? go margins_set 30 30 10 10
!  yes? plot/nolab uwnd[i=@ave,j=@ave]
!  yes? plot/over/color=2/thick=2/DASH=(0.1,0.05,0.1,0.05)/nolab uwnd[i=@ave,j=@ave,l=@sbx:12]
!  yes? go text_legend_put2 50 80 80 " Smoothed" /color=2/DASH=(0.1,0.05,0.1,0.05)/thick=2 " " uwnd
!
! Notes:
!  * Use @Cnnn (where nnn is the pen number) before the text
!  to get a colored text
!  For example "@C008This a red text and thick"
!  * Add a blank before text to get a space between the line
!  and the text
!  * Caracter height is expressed in cm (default=0.3)  
!  * ME_qualifiers example
!		/line/color=1  (default)
!		/line/color=2/thick=2  
!		/color=1/DASH=(0.1,0.05,0.1,0.05)
!		/color=2/DASH=(0.1,0.05,0.1,0.05)/thick=2
!
! Calls: 
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/07 15:03:44 $ 
! $Name: FAST_672_1_0 $
! $Revision: 1.1.1.15 $
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go text_legend_put2 xpos1 xpos2 ypos "text" [PLOT qualifiers] [character height=0.3] [time variable]%
query/ignore $2%<Usage: go text_legend_put2 xpos1 xpos2 ypos "text" [PLOT qualifiers] [character height=0.3] [time variable]%
query/ignore $3%<Usage: go text_legend_put2 xpos1 xpos2 ypos "text" [PLOT qualifiers] [character height=0.3] [time variable]%
query/ignore $4%<Usage: go text_legend_put2 xpos1 xpos2 ypos "text" [PLOT qualifiers] [character height=0.3] [time variable]%

def sym ME_xpos1=`($VP_WIDTH) * ($1)/100 - ($PPL$XORG)`
def sym ME_xpos2=`($VP_WIDTH) * ($2)/100 - ($PPL$XORG)`
def sym ME_ypos=`($VP_HEIGHT) * ($3)/100 - ($PPL$YORG)`
def sym ME_qualifiers=$5%/line/color=1%
def sym ME_labheight=`$6%0.3%/2.54`   ! cm to inch
def sym ME_just=-1	! left 
def sym ME_angle=0		

! text
label/nouser ($ME_xpos2),($ME_ypos),($ME_just),($ME_angle),($ME_labheight), "$4"

!-----------------------------------
! Relative to bug err551_XAXIS_1.jnl
def sym ME_XAXIS_VAR=$7%XXXXX%

if ($ME_XAXIS_VAR%|XXXXX>true|*>false|%) then
        def sym ME_XAXIS_MIN=($XAXIS_MIN)
        def sym ME_XAXIS_MAX=($XAXIS_MAX)
else
        let ME_XAXIS_VAR_T=t[gt=($ME_XAXIS_VAR)]
	def sym ME_XAXIS_MIN=`ME_XAXIS_VAR_T[l=1]`
        def sym ME_XAXIS_LEND=`ME_XAXIS_VAR_T,return=lend`
	def sym ME_XAXIS_MAX=`ME_XAXIS_VAR_T[l=($ME_XAXIS_LEND)]`
endif
!-----------------------------------

! line made with plot command (waiting for qualifiers /nouser)
ppl window,off
let ME_xslope=((($ME_XAXIS_MAX))-(($ME_XAXIS_MIN)))/($PPL$XLEN)
let ME_yslope=((($YAXIS_MAX))-(($YAXIS_MIN)))/($PPL$YLEN)

let ME_x1=(($ME_XAXIS_MIN))+ME_xslope*(($ME_xpos1))
let ME_x2=(($ME_XAXIS_MIN))+ME_xslope*(($ME_xpos2))
let ME_y1=(($YAXIS_MIN))+ME_yslope*(($ME_ypos))
let ME_y2=(($YAXIS_MIN))+ME_yslope*(($ME_ypos))

plot/over/vs/nolab($ME_qualifiers) {`ME_x1`,`ME_x2`},{`ME_y1`,`ME_y2`}

cancel var ME_*

! return to default
ppl window,on

!**************************************************************  
cancel sym ME_*

!**************************************************************
set mode/last verify
