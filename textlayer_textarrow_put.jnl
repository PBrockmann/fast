\cancel mode verify

!**************************************************************
! Description: Put a text and an arrow at a position expressed as percentage 
!  of width and height of the page 
!
! Usage: go textlayer_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]
!
! Example: 
!  yes? go page_new 1 2
!  yes? set v 1
!  yes? plot i[i=1:100]
!  yes? go textlayer_new
!  yes? go textlayer_textarrow_put 80 40 50 80 "textright     _" 1
!  yes? go textlayer_textarrow_put 50 40 40 80 "textcentered" 0 
!  yes? go textlayer_textarrow_put 30 40 60 80 "       textleft" 1
!
! Notes:
!  * Justification: -1 left, 0 center, +1 right 
!  * Use @Cnnn (where nnn is the pen number) before the text
!  to get a colored text
!  For example "@C008This a red text and thick"
!  * Pen numbers range from 1 to 18 and follow the formula:
!          pen = 6 * (thickness - 1) + color
!  with color = 1,2,3,4,5,6 = black,red,green,blue,cyan,magenta
!  and thickness = 1,2,3
!  * Add a blank before or after text to get spaces between 
!  the arrow and the text; to add blanks after the text, use
!  the special character '_' (lowerscript)
!  yes? go textlayer_arrow_click "a text with blanks after     _"
!  * Caracter height is expressed in cm (default=0.3)
!  * Angle at which text is to be drawn  
!  (0 degrees is at 3 o'clock and positive rotation 
!  is counter clockwise.)
!  * Arrow size head is expressed in cm
!
! Calls: 
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/07 15:03:09 $ 
! $Name: FAST_672_1_0 $
! $Revision: 1.1.1.27 $
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go textlayer_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $2%<Usage: go textlayer_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $3%<Usage: go textlayer_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%
query/ignore $4%<Usage: go textlayer_textarrow_put xpos1 ypos1 xpos2 ypos2 "text" [justification=0] [character height=0.3] [angle=0] [arrow size head=0.3]%

!**************************************************************
def sym ME_ratio = `($PPL$XLEN)/($PPL$YLEN)`
 
!**************************************************************
def sym ME_xpos1=`$1/100`
def sym ME_ypos1=`$2/100`
def sym ME_xpos2=`$3/100`
def sym ME_ypos2=`$4/100`

def sym ME_text=$5
def sym ME_just=$6%0%
def sym ME_labheight=`$7%0.3%/2.54`   ! cm to inch
def sym ME_angle=$8%0%

! text
if `($ME_ypos1) lt ($ME_ypos2)` then 
  label ($ME_xpos1),`($ME_ypos1)-2*($ME_labheight)/($PPL$YLEN)`,($ME_just),($ME_angle),($ME_labheight),"($ME_text)"
else
  label ($ME_xpos1),`($ME_ypos1)+($ME_labheight)/($PPL$YLEN)`,($ME_just),($ME_angle),($ME_labheight),"($ME_text)"
endif

! arrow
!**************************************************************
let ME_PI = atan(1.)*4.
let ME_DEG2RAD = ME_PI/180.

!**************************************************************
def sym ME_nbvectors=1

let ME_x1 = ($ME_xpos1)
let ME_y1 = ($ME_ypos1) 
let ME_x2 = ($ME_xpos2)
let ME_y2 = ($ME_ypos2) 
let ME_u = `($ME_xpos2) - ($ME_xpos1)` 
let ME_v = `($ME_ypos2) - ($ME_ypos1)`

!-----------------------
! vector points from x1,y1 with components u,v
!           2 
!         / | \
!        a  |  b
!           | 
!           | 
!           | 
!           1 
!-----------------------
def sym ME_head_size=$9%0.3%	        ! in cm
let ME_head_angle = 30*ME_DEG2RAD       ! in degrees
let ME_size = `($ME_head_size)/(($PPL$XLEN)*2.54)`
let ME_alpha = atan2(ME_v/($ME_ratio),ME_u)

!-- point a
let ME_head_xa0 = cos(ME_alpha - ME_head_angle) * ME_size * 1/cos(ME_head_angle)
let ME_head_ya0 = sin(ME_alpha - ME_head_angle) * ME_size * 1/cos(ME_head_angle)

let ME_head_xa1 = ME_x2 - ME_head_xa0  
let ME_head_ya1 = ME_y2 - ME_head_ya0 * ($ME_ratio)

!-- point b
let ME_head_xb0 = sin(ME_alpha + ME_head_angle - ME_PI/2.) * ME_size * 1/cos(ME_head_angle)
let ME_head_yb0 = cos(ME_alpha + ME_head_angle - ME_PI/2.) * ME_size * 1/cos(ME_head_angle)

let ME_head_xb1 = ME_x2 + ME_head_xb0 
let ME_head_yb1 = ME_y2 - ME_head_yb0 * ($ME_ratio)

! arrow color
ppl pen 1,($ME_linecolor)

! line s-e
ppl aline 1,($ME_xpos1),($ME_ypos1),($ME_xpos2),($ME_ypos2)

! line e-a
ppl aline 1,($ME_xpos2),($ME_ypos2),`ME_head_xa1`,`ME_head_ya1`

! line e-b
ppl aline 1,($ME_xpos2),($ME_ypos2),`ME_head_xb1`,`ME_head_yb1`

! line a-b
!ppl aline 1,`ME_head_xa1`,`ME_head_ya1`,`ME_head_xb1`,`ME_head_yb1`

! Return to normal
ppl pen 1,1
 
!**************************************************************
cancel sym ME_*
cancel var ME_*

!**************************************************************
set mode/last verify
