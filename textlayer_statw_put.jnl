\cancel mode verify

!**************************************************************
! Description: Put a statistic text at a position expressed as percentage 
!  of width and height of the page
!
! Usage: go textlayer_statw_put xpos ypos var model [justification=0] [character height=0.3] [angle=0]
!
! Example: 
!
! Notes:
!  * Justification: -1 left, 0 center, +1 right 
!  * Use @Cnnn (where nnn is the pen number) before the text
!  to get a colored text
!  For example "@C008This a red text and thick"
!  * Pen numbers range from 1 to 18 and follow the formula:
!          pen = 6 * (thickness - 1) + color
!  with color = 1,2,3,4,5,6 = black,red,green,blue,cyan,magenta
!  and thickness = 1,2,3
!  * Caracter height is expressed in cm (default=0.3)
!  * Angle at which text is to be drawn  
!  (0 degrees is at 3 o'clock and positive rotation 
!  is counter clockwise.)
!
! Calls: go textlayer_text_put
!
! Author: Patrick Brockmann 
! Contact: Patrick.Brockmann@ipsl.jussieu.fr
! $Date: 2011/09/07 15:03:11 $ 
! $Name: FAST_672_1_0 $
! $Revision: 1.1.1.9 $
! History:
! Modification:
!
!**************************************************************

query/ignore $1%<Usage: go textlayer_statw_put xpos ypos var model [justification=0] [character height=0.3] [angle=0]%
query/ignore $2%<Usage: go textlayer_statw_put xpos ypos var model [justification=0] [character height=0.3] [angle=0]%
query/ignore $3%<Usage: go textlayer_statw_put xpos ypos var model [justification=0] [character height=0.3] [angle=0]%

!**************************************************************

set data/save

use "($model)_grid.nc"

!**************************************************************
def sym ME_xpos=$1
def sym ME_ypos=$2
def sym ME_just=$5%0%
def sym ME_labheight=$6%0.3%
def sym ME_angle=$7%0%

let ME_var=xsequence(($3))
let ME_weight_init=area[d=$4_grid.nc]
let ME_weight=xsequence(ME_weight_init)
let ME_varw=ME_var*ME_weight

def sym ME_varmin=`ME_var[i=@min],P=-3`
def sym ME_varmax=`ME_var[i=@max],P=-3`

def sym ME_varavg=`ME_var[i=@sum]/ME_var[i=@ngd],P=-3`
def sym ME_varvar=`ME_var[i=@var],P=-3`
def sym ME_varstd=`ME_var[i=@var]^0.5,P=-3`

let ME_varwavg=ME_varw[i=@sum]/ME_weight[i=@sum]
let ME_varw_diff=(ME_var-ME_varwavg)^2
def sym ME_varwavg=`ME_varw[i=@sum]/ME_weight[i=@sum],P=-3`
def sym ME_varwvar=`ME_varw_diff[i=@sum]/ME_varw_diff[i=@ngd],P=-3`
def sym ME_varwstd=`(ME_varw_diff[i=@sum]/ME_varw_diff[i=@ngd])^0.5,P=-3`


def sym ME_varngd=`ME_var[i=@ngd],P=-3`
def sym ME_varnbd=`ME_var[i=@nbd],P=-3`
def sym ME_varn=`($ME_varngd)+($ME_varnbd),P=-3`
def sym ME_varngd_p=`100*($ME_varngd)/($ME_varn),P=-3`
def sym ME_varnbd_p=`100*($ME_varnbd)/($ME_varn),P=-3`

! text (@AS to get special characters like %)
def sym ME_textline1=Min: ($ME_varmin)  Max: ($ME_varmax)
def sym ME_textline2=Avg: ($ME_varavg)  Var: ($ME_varvar) Std: ($ME_varstd)
def sym ME_textline3=W.Avg: ($ME_varwavg)  W.Var: ($ME_varwvar) W.Std: ($ME_varwstd)
def sym ME_textline4=@ASNpt: ($ME_varn) Ngd: ($ME_varngd) (($ME_varngd_p)%) Nbd: ($ME_varnbd) (($ME_varnbd_p)%)

def sym ME_textlines=($ME_textline1)<NL>($ME_textline2)<NL>($ME_textline3)<NL>($ME_textline4)

go textlayer_text_put.jnl ($ME_xpos) ($ME_ypos) "($ME_textlines)" ($ME_just) ($ME_labheight) ($ME_angle)

!**************************************************************
cancel data ($model)_grid.nc

set data/restore

!**************************************************************
cancel var ME_*
cancel sym ME_*

!**************************************************************
set mode/last verify
